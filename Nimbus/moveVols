#! /bin/tcsh
# Script: MOVEVOLS
# Function:
#          All motion corrected (& time sliced) 3D images, for all scanning sessions, are in the 
#          top level subject directories (e.g., ow1). This script partitions this set of files and
#          moves them into their appropriate scan (or session) directories.  See the NOTES section
#          below for more details.
# 
# Created by Christopher Currie
# Columbia University
# 03 / 04 / 04
#
# NOTES:
# This script addresses a problem where we merge all the timeslice corrected 3d images files in each
# scan directory into one big 3d image file.  We then McFlirt that and then use avwsplit to create 
# vol?????.img and .hdr files where ?????=00000 to whatever total number of TRs there are across all 
# scanning sessions.
# This script "figures out" which scanning session (or scan directory -- there's 8, scan1, ... , scan8)
# a given vol?????.,img file belongs to, renames it (i.e., it adds a subject code and scan session 
# number), and then moves it into that scan directory.
#
if ($#argv < 1) then
 echo "$0 subjcode"
  echo "please type in: process_subject sidcode"
  echo "sidcode is subject id, e.g. ow1"
  exit 1
else
  set exptname = $argv[1]
endif

set mdir = "/export/data/analyzed/tor/Class/$exptname"
echo Changing to directory $mdir 
cd $mdir
#
# get a list of all the scan directories
#
set d = `ls -d scan?`
echo found scan directories: $d
@ start = 0
#
# get a list of all the motion corrected volumes for the subject (over all the runs or scanning sessions)
#
set flist = `ls vol*.img`
foreach dir ( $d )
   #
   # get the  number at the end of the directory name, i.e., "1" at the end of "scan1"
   #
   set dirNum=`echo $dir | cut -c 5-5`
   #
   # count all the ar files (motion correct) volumes in a scan directory
   #
   set trCnt = `ls -l scan$dirNum/ar$dirNum\_????.img | grep "^-" | wc -l`
   #
   # "cnt"  will serve as an index to each file in $flist
   # "start" and "end" will bound "cnt" such that if start <= cnt <= end, then
   # the indexed file in $flist will be renamed and moved into whatever
   # scan directory $dir is pointing to.
   #
   @ end = $start + $trCnt - 1
   echo $start to $end
   @ cnt = 0
   #
   # Work my way through $flist 
   foreach f ( $flist )
                @ cnt+=1
                 if ( $cnt >= $start && $cnt <= $end) then
                     # echo changing $f to $exptname\_$dir\_$f
                     cp $f $exptname\_$dir\_$f
                     # echo move $exptname\_$dir\_$f to $dir
                     mv $exptname\_$dir\_$f $dir
                 endif
   end
   @ start = $start + $trCnt
end
echo Finished moveing $exptname volume images into scanning directories
